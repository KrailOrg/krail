/*
 *
 *  * Copyright (c) 2016. David Sowerby
 *  *
 *  * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 *  * the License. You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 *  *
 *  * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 *  * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 *  * specific language governing permissions and limitations under the License.
 *
 */

//apply plugin: 'war' // needed only for providedCompile

buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
        classpath 'commons-io:commons-io:2.4'
        classpath 'uk.q3c:q3c-gradle:0.7.3'
    }
}

plugins {
    id 'groovy'
    id 'jacoco'
    id 'project-report'
    id 'com.github.kt3k.coveralls' version '2.4.0x'
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java'
apply plugin: 'eclipse-wtp'
apply plugin: 'idea'
apply plugin: 'q3c-release'

description = 'A rapid development framework for web apps using Vaadin, Guice, Shiro et al'

ext.assertjVersion = '3.3.0'
ext.commonsLangVersion = '3.3.2'
ext.groovyVersion = '2.4.1'
ext.guiceVersion = '4.0'
ext.shiroVersion = '1.2.4'
ext.slf4jVersion = '1.7.6'
ext.vaadinVersion = '7.6.3'
ext.versionDescription = 'No version description provided'

ext.releaseDate = new Date()

group = 'uk.q3c.krail'
version = '0.9.10.22'
//version = versionFromFile(project.projectDir)

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'uk.q3c.krail:krail-option:0.10.0.0.9e825e2'
    compile 'uk.q3c.krail:krail-config-api:0.9.0.1.1a175d3'


    //Dependency Injection
    compile 'com.google.inject.extensions:guice-servlet:' + guiceVersion
    compile 'com.google.inject.extensions:guice-assistedinject:' + guiceVersion




    //groovy
    compile 'org.codehaus.groovy:groovy-all:' + groovyVersion

    //logging
    compile 'org.slf4j:slf4j-api:' + slf4jVersion
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile 'ch.qos.logback:logback-core:1.1.2'

    //servlet // switching to providedCompile broke mock for VaadinSession
    compile "javax.servlet:javax.servlet-api:3.+"

    //Utility
    //the easiest way to all the Jung parts
    compile 'net.sf.jung:jung-samples:2.0.1'
    compile 'org.apache.commons:commons-lang3:' + commonsLangVersion
    compile 'commons-io:commons-io:2.4'

    compile 'org.reflections:reflections:0.9.10'
    compile 'net.jodah:typetools:0.4.3'

    //Validation
    compile 'org.apache.bval:bval-jsr303:0.5'
    compile 'org.apache.bval:bval-guice:0.5'
    compile 'javax.validation:validation-api:1.0.0.GA'

    //User access control
    compile 'org.apache.shiro:shiro-core:' + shiroVersion
    compile 'org.apache.shiro:shiro-web:' + shiroVersion
    compile 'org.apache.shiro:shiro-guice:' + shiroVersion

    //Vaadin

    //The core server part of Vaadin -->
    compile 'com.vaadin:vaadin-server:' + vaadinVersion
    compile 'com.vaadin:vaadin-push:' + vaadinVersion


    //included in compile to enable the production of test utils
    compile 'com.mycila.testing.plugins:mycila-testing-mockito:2.8'
    compile 'org.mockito:mockito-all:1.10.19'
    compile 'org.spockframework:spock-core:1.0-groovy-2.4'

    //testing

    testCompile 'org.eclipse.persistence:javax.persistence:2.1.0'
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:' + assertjVersion
    testCompile 'org.assertj:assertj-guava:1.3.1'
    testCompile 'com.mycila.testing.plugins:mycila-testing-mockito:2.8'
    testCompile 'com.mycila:mycila-guice:2.10.ga'
    testCompile 'com.mycila.testing.plugins:mycila-testing-guice:2.8'
    testCompile 'org.mockito:mockito-all:1.10.19'

    testCompile 'org.spockframework:spock-guice:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:3.2.0' // needed for Spock mocking
//    testCompile "javax.servlet:javax.servlet-api:3.+"



    testCompile 'uk.q3c:q3c-testUtil:0.8.1'
}

bintray {
    dryRun = false
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

jacocoTestReport.reports.xml.enabled = true


publishing {
    publications {
        mavenStuff(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

javadoc {
    failOnError = false
}

task verifyRelease(type: uk.q3c.gradle.VerificationTask) {

}

task prepareRelease(type: uk.q3c.gradle.PrepareReleaseTask) {

}

task release(type: uk.q3c.gradle.ReleaseTask) {

}

bintray {
    user = 'dsowerby'
    key = getKeyForBintray()

    publications = ['mavenStuff'] //When uploading Maven-based publication files
    dryRun = false  //project.ext.bintrayDryRun //Whether to run this as dry-run, without deploying
    publish = true //If version should be auto published after an upload
    pkg {
        repo = 'maven'
//        userOrg = 'myorg' //An optional organization name when the repo belongs to one of the user's orgs
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/davidsowerby/' + project.name
        issueTrackerUrl = websiteUrl + '/issues'
        vcsUrl = websiteUrl + '.git'
        licenses = ['Apache-2.0']
        labels = ['test']
        publicDownloadNumbers = true
        //Optional version descriptor
        version {
            name = project.version
            desc = project.ext.versionDescription
            released = project.ext.releaseDate
            //'optional, date of the version release' //2 possible values: date in the format of 'yyyy-MM-dd'T'HH:mm:ss.SSSZZ' OR a java.util.Date instance
            mavenCentralSync {
                sync = false //Optional (true by default). Determines whether to sync the version to Maven Central.
            }
        }
    }
}

def versionFromFile(File projectRoot) {
    Properties props = new Properties()
    def propFile = new File(projectRoot, 'version.properties')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
    }
    String key = "CURRENT_VERSION"
    println(projectRoot.getName() + ": version is " + props[key])
    props[key]
}
//Need to identify whether platform has the Bintray key (Travis etc will not)
def getKeyForBintray() {
    def keyValue = System.getenv("BINTRAY")
    if (keyValue == null) {
        println 'returning no bintray key'
        return 'no bintray key'
    } else {
        println 'returning bintray key: '
        return bintrayKey
    }
}