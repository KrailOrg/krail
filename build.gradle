/*
 * Copyright (c) 2014 David Sowerby
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.
 */

//build for main Krail project

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

sourceCompatibility = '1.8'

version = '0.7.3-draft'

project.ext.vaadinVersion = '7.3.6'
project.ext.guiceVersion = '3.0'
project.ext.shiroVersion = '1.2.1'


project.ext.exportDir = System.getProperty("user.home") + "/temp/jars/" + project.name

jar {
    libsDirName = exportDir
    println("jar exported to:  " + exportDir)
}

task writeNewPom << {
    pom {
        groupId = "uk.q3c.krail"
        project {
            inceptionYear '2014'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }.writeTo(exportDir + "/" + project.name + "-" + version + ".pom")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}



task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task publish(dependsOn: ['sourcesJar', 'javadocJar', 'jar', 'writeNewPom']) {


}

artifacts {
    archives sourcesJar
    archives javadocJar
}

javadoc {
    failOnError = false
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {

    //Dependency Injection
    compile 'com.google.inject:guice:' + guiceVersion
    compile 'com.google.inject.extensions:guice-servlet:' + guiceVersion
    compile 'com.google.inject.extensions:guice-assistedinject:' + guiceVersion
    compile 'com.google.inject.extensions:guice-multibindings:' + guiceVersion

    //Date Time
    compile 'joda-time:joda-time:2.1'

    //Logging
    compile 'org.slf4j:slf4j-api:1.6.6'
    compile 'org.slf4j:slf4j-log4j12:1.6.6'

    //testing
    testCompile 'uk.q3c.util:testUtil:0.7.0'
    testCompile 'com.mycila:mycila-guice:2.10.ga'
    testCompile 'com.mycila.testing.plugins:mycila-testing-guice:2.8'
    testCompile 'com.mycila.testing.plugins:mycila-testing-mockito:2.8'
    testCompile 'org.mockito:mockito-all:1.9.0'
    testCompile 'junit:junit:4.11'
    testCompile 'org.assertj:assertj-core:1.5.0'
    testCompile 'org.assertj:assertj-joda-time:1.1.0'
    testCompile 'org.assertj:assertj-guava:1.0.0'

    //servlet
    compile "javax.servlet:javax.servlet-api:3.+"

    //Testing
//    testCompile 'com.googlecode.jmockit:jmockit:1.0'

    //Utility
    compile 'com.google.guava:guava:13.0.1'
    //the easiest way to all the Jung parts
    compile 'net.sf.jung:jung-samples:2.0.1'
    compile 'org.apache.commons:commons-lang3:3.1'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-configuration:commons-configuration:1.10'
    compile 'org.reflections:reflections:0.9.9-RC1'

    //Validation
    compile 'org.apache.bval:bval-jsr303:0.5'
    compile 'org.apache.bval:bval-guice:0.5'
    compile 'javax.validation:validation-api:1.0.0.GA'

    //User access control
    compile 'org.apache.shiro:shiro-core:' + shiroVersion
    compile 'org.apache.shiro:shiro-web:' + shiroVersion
    compile 'org.apache.shiro:shiro-guice:' + shiroVersion

    //Vaadin

    //The core server part of Vaadin -->
    compile 'com.vaadin:vaadin-server:' + vaadinVersion
    // needed for Vaadin javadoc only
    compile "javax.portlet:portlet-api:2.0"
    compile "org.atmosphere:atmosphere-runtime:2.1.6"


}
